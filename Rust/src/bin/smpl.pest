Document = { SOI ~ NEWLINE* ~ Imports ~ Variables ~ NEWLINE* ~ SubBlock ~ NEWLINE* ~ Block* ~ NEWLINE* ~ EOI }

// WHITESPACE = _{ " " }
COMMENT = _{
    ("/*" ~ (!"*/" ~ ANY)* ~ "*/")
  | (" "* ~ ("//" ~ (!NEWLINE ~ ANY)*))
  | (" "* ~ ("#" ~ (!NEWLINE ~ ANY)*))
}

Name = @{ (ASCII_ALPHANUMERIC | "_")+ }
Number = @{ "-"? ~ ASCII_DIGIT+ }
Filepath = @{ ("." | "/" | Name)+ }

Imports = { (Import ~ NEWLINE+)* }
Import = { "use" ~ SEPARATOR ~ Name ~ SEPARATOR ~ Filepath }

Variables = { (Variable ~ NEWLINE+)* }
Variable = { "var" ~ SEPARATOR ~ Name ~ SEPARATOR ~ VarType ~ NEWLINE }
VarType = { "list" | "num" }

Block = { "label" ~ SEPARATOR ~ Name ~ NEWLINE+ ~ SubBlock }
SubBlock = { COMMENT | (Expr ~ NEWLINE+)* }

Push = { "push" }
Pop = { "pop" }
Not = { "not" }
Add = { "add" }
Greater = { "greater" }
Sub = { "sub" }
Div = { "div" }
Mod = { "mod" }
Mul = { "mul" }
Dup = { "dup" }
InN = { "inN" }
InC = { "inC" }
Goto = { "goto" }
Branch = { "branch" }
Debug = { "debug" }
OutC = { "outC" }
OutN = { "outN" }
Roll = { "roll" }

Get = { "get" }
Set = { "set" }

LibFun = @{"lib_" ~ Name }

Expr = {
  | Push ~ SEPARATOR ~ Number
  | Pop
  | Not
  | Add
  | Greater | Sub | Div | Mod | Mul
  | Dup
  | InN
  | InC
  | Goto ~ SEPARATOR ~ Name
  | Branch ~ SEPARATOR ~ Name ~ SEPARATOR ~ Name
  | Debug
  | OutC
  | OutN
  | Roll

  | Set ~ SEPARATOR ~ Name
  | Get ~ SEPARATOR ~ Name

  | LibFun
}

// "append"
// "get_heap"
// "set_heap"
// "get_list"
// "print_listC"
// "print_listN"
// "readC_until"
// "readlines"
// "printC_list_of_list"
// "malloc"
// "length"
// "copy_memory"
